<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" title="Creators Project" backgroundColor="0xffffff" width="1360" height="768"
	xmlns:textures="com.creatorsproject.ui.textures.*" xmlns:chips="com.creatorsproject.ui.chips.*">

	<mx:Style>
		
		.chip {
			horizontal-center: 0;
			vertical-center: 0;
		}
		
		.chipBody {
			background-color: #000000;
			paddingTop: 40;
			paddingBottom: 40;
			paddingLeft: 40;
			paddingRight: 40;
		}
		
		.bigTitle {
			font-family: "Neo Sans Intel";
			font-size: 48;
			color: #0079bc;
		}
		
		.mediumTitle {
			font-family: "Neo Sans Intel";
			color: #0079bc;
			font-size: 32px;
		}
		
		.smallTitle {
			font-family: "Neo Sans Intel";
			color: #ffffff;
			font-size: 18px;
		}
		
		.detailText {
			font-family: "Neo Sans Intel Light";
			color: #ffffff;
			font-size: 16;
			letterSpacing: 1;
		}
		
		.touchButton {
			font-family: "Neo Sans Intel";
			paddingLeft: 10;
			paddingRight: 10;
			paddingTop: 10;
			paddingBottom: 10;
			letterSpacing: 3;
			highlightAlphas: 0, 0;
			fillAlphas: 1, 1, 1, 1;
			fillColors: #222222, #222222, #222222, #222222;
			color: #aaaaaa;
			textRollOverColor: #0b333c;
			textSelectedColor: #000033;
			borderColor: #000000;
			fontSize: 14;
			fontWeight: normal;
			cornerRadius: 0;
		}
		
		.headerText {
			font-family: "Neo Sans Intel Light";
			font-size: 24;
			color: #ffffff;
		}
		
		.creatorText {
		   fontFamily: Arial;
		   fontSize: 16;
		   color: #ffffff;
		   letterSpacing: 1;
		   backgroundColor: #000000;
		   borderStyle: none;
		}
		
	</mx:Style>

	<mx:Script>
		<![CDATA[
			import com.creatorsproject.ui.Tab;
			import org.papervision3d.core.proto.MaterialObject3D;
			import org.papervision3d.objects.primitives.Plane;
			import com.creatorsproject.ui.MapMarker;
			import com.creatorsproject.ui.MapUI;
			import com.creatorsproject.data.DataConstants;
			import com.creatorsproject.ui.TouchUI;
			import org.papervision3d.core.math.Number3D;
			import com.creatorsproject.input.events.GestureEvent;
			import com.creatorsproject.input.TouchController;
			import com.creatorsproject.ui.CreatorUI;
			import com.creatorsproject.ui.ScheduleMarker;
			import org.papervision3d.materials.MovieMaterial;
			import org.papervision3d.objects.DisplayObject3D;
			import com.creatorsproject.geom.TileBand;
			import org.papervision3d.core.geom.renderables.Vertex3D;
			import com.creatorsproject.ui.EventUI;
			import com.creatorsproject.data.PartyData;
			import com.creatorsproject.data.EventRoom;
			import com.creatorsproject.data.PartyEvent;
			import com.creatorsproject.ui.ITickable;
			import org.papervision3d.materials.utils.MaterialsList;
			import org.papervision3d.materials.ColorMaterial;
			import org.papervision3d.objects.primitives.Cube;
			import org.papervision3d.cameras.Camera3D;
			import org.papervision3d.scenes.Scene3D;
			import org.papervision3d.render.BasicRenderEngine;
			import org.papervision3d.view.Viewport3D;
			
			// Assets!
			[Embed(source="../media/img/creators_project_inverted_small.png")]
			[Bindable]
			public static var creatorLogo:Class;
			
			[Embed(source="../media/img/intel_vice_logo_lockup_small.png")]
			[Bindable]
			public static var viceLogo:Class;
			
			// Singletoning!
			public static var instance:main;
		
			// 3D!
			private var _viewport:Viewport3D;
			private var _renderer:BasicRenderEngine;
			private var _scene:Scene3D;
			private var _camera:Camera3D;
			
			// Interaction!
			private var _touchController:TouchController;
			private var _moduleAnchors:Dictionary;
			private var _focusedModule:TouchUI;
			
			// Data!
			private var _partyData:PartyData;
			
			// Interface!
			public static var EVENT_NODE:String = "eventNode";
			public static var CREATOR_NODE:String = "creatorNode";
			public static var MAP_NODE:String = "mapNode";
			private var _eventUI:EventUI;
			private var _creatorUI:CreatorUI;
			private var _mapUI:MapUI;
			
			private var _camMax:Number;
			private var _camMin:Number;
			private var _camTarget:Number = 0;
			private var _camState:String = "creator";
			
			private var tab1:Tab;
			private var tab2:Tab;
			
			// ____________________________________________ Application Setup
			
			public function init(isLocal:Boolean = true):void {
				DataConstants.videoDir = new File();
				DataConstants.videoDir.browseForDirectory("Where are the video files?");
				
				DataConstants.isLocal = isLocal;
				initWindow.visible = false;
				initSever();
				//animationTest();
			}
			
			public function initSever():void {
				instance = this;
				_partyData = new PartyData();
				_partyData.loadData();
				_partyData.addEventListener(Event.COMPLETE, this.onDataSuccess);
				_partyData.addEventListener(IOErrorEvent.IO_ERROR, this.onPartyDataError);
			}
			
			public function onPartyDataError(event:IOErrorEvent):void {
				
			}
			
			public function onDataSuccess(event:Event):void {
				this.initTouch();
				this.init3D();
				this.initInterface();
			}
			
			public function init3D():void {
				_viewport = new Viewport3D(0, 0, true, true);
				_renderer = new BasicRenderEngine();
				_scene = new Scene3D();
				_camera = new Camera3D(50);
				
				this.backUI.addChild(_viewport);
				this.addEventListener(Event.ENTER_FRAME, this.onRenderTick); 
			}
			
			public function initTouch():void {
				TouchController.me.setup(this.stage, this.clickMatte);
				TouchController.me.addEventListener(GestureEvent.SWIPE_START, onSwipe);
				TouchController.me.addEventListener(GestureEvent.SWIPE, onSwipe);
				TouchController.me.addEventListener(GestureEvent.SWIPE_END, onSwipe);
			}
			
			public function initInterface(event:Event = null):void {
				
				_mapUI = new MapUI(_partyData);
				_scene.addChild(_mapUI);
				setUIPosition(_mapUI, 14);
				
				_creatorUI = new CreatorUI(_partyData);
				_scene.addChild(_creatorUI);
				setUIPosition(_creatorUI, 0);
				
				_eventUI = new EventUI(_partyData);
				_scene.addChild(_eventUI);
				setUIPosition(_eventUI, -14);
				
				_camMax = 30;
				_camMin = -30;
				
				tab1 = new Tab();
				tab1.setStyle("horizontalCenter", 0);
				tab1.direction = "up";
				tab1.text = "Schedule";
				tab1.addEventListener(MouseEvent.CLICK, onTabClick);
				
				tab2 = new Tab();
				tab2.setStyle("horizontalCenter", 0);
				tab2.direction = "down";
				tab2.text = "Map";
				tab2.addEventListener(MouseEvent.CLICK, onTabClick);
				
				this.frontUI.addChild(tab1);
				this.frontUI.addChild(tab2);
				
				this.positionTabs(_camTarget);
				
				
				// finally, get the camera into place
				_camera.z = -5000;
			}
			
			private function dump():void {
				var step:Number = Math.PI / 20;
				var mat:ColorMaterial = new ColorMaterial();
				for(var i:int = 0; i < 40; i ++) {
					var cube:Cube = new Cube(new MaterialsList({all: mat}), 50, 50, 50);
					cube.z = -2000 * Math.cos(step * i);
					cube.y = -2000 * Math.sin(step * i);
					_scene.addChild(cube);
				}
			}
			
			private function setUIPosition(ui:DisplayObject3D, theta:Number):void {
				var thetaRad:Number = theta * Math.PI / 180;
				
				ui.z = -2000 * Math.cos(thetaRad);
				ui.y = -2000 * Math.sin(thetaRad);
				
				ui.rotationX = -theta;
			}
			
			// ____________________________________________ Rendering and Updating
			
			public function onRenderTick(event:Event):void {
				for each(var t:DisplayObject3D in _scene.children) {
					if(t is ITickable) {
						(t as ITickable).tick();
					}
				}
				_renderer.renderScene(_scene, _camera, _viewport);
				
				if(this.currentTime) {
					currentTime.text = (new Date()).toLocaleTimeString();
					
				}
			}
			
			public function toggleFullscreen():void {
				if(stage.displayState == StageDisplayState.NORMAL) {
					stage.displayState = StageDisplayState.FULL_SCREEN_INTERACTIVE;
					this.showStatusBar = false;
					this.fullScreenButton.visible = false;
				} else {
					stage.displayState = StageDisplayState.NORMAL;
				}
			}
			
			// ____________________________________________ Interaction
			
			private function onSwipe(event:GestureEvent):void {
				
				if(event.type == GestureEvent.SWIPE_START) {
					this.removeEventListener(Event.ENTER_FRAME, onResetCamera);
				}
				
				if(event.type == GestureEvent.SWIPE) {
					var c:Number = _camera.rotationX + (event.delta.y / 20);
					
					var eventDelta:Number = Math.abs(_eventUI.rotationX - c);
					var creatorDelta:Number = Math.abs(_creatorUI.rotationX - c);
					var mapDelta:Number = Math.abs(_mapUI.rotationX - c);
					
					if(mapDelta == Math.min(mapDelta, eventDelta, creatorDelta)) {
						_camTarget = -14;
						_camState = "map";
					} else if(creatorDelta == Math.min(mapDelta, eventDelta, creatorDelta)) {
						_camTarget = 0;
						_camState = "creator";
					} else {
						_camTarget = 14;
						_camState = "event";
					}
					
					setTabStrings();
					
					if(eventDelta < 7 && _eventUI.state == "disable") {
						_eventUI.state = "floors";
					} else if(creatorDelta < 7 && _creatorUI.state == "disable") {
						_creatorUI.state = "creators";
					} else if(mapDelta < 7 && _mapUI.state == "disable") {
						_mapUI.state = "overview";
					}
					
					this.setCamLocation(c);
					
					// tabmotion
				}
				
				if(event.type == GestureEvent.SWIPE_END) {
					this.addEventListener(Event.ENTER_FRAME, onResetCamera);
					cleanInterface();
				}
				
			}
			
			private function onResetCamera(event:Event):void {
				if(Math.abs(_camTarget - _camera.rotationX) < .1) {
					this.setCamLocation(_camTarget);
					this.removeEventListener(Event.ENTER_FRAME, onResetCamera);
					
				} else {
					var c:Number = _camera.rotationX + (_camTarget - _camera.rotationX) / 5;
					this.setCamLocation(c);
				}
			}
			
			private function cleanInterface():void {
				switch(_camState) {
					case "map":
						//_mapUI.state = "overview";
						_creatorUI.state = "disable";
						_eventUI.state = "disable";
						break;
					case "creator":
						//_creatorUI.state = "creators";
						_eventUI.state = "disable";
						_mapUI.state = "disable";
						break;
					case "event":
						//_eventUI.state = "floors";
						_creatorUI.state = "disable";
						_mapUI.state = "disable";
						break;
				}
			}
			
			private function setCamLocation(theta:Number):void {
				_camera.rotationX = theta;
				_camera.z = -5000 * Math.cos(theta * Math.PI / 180);
				_camera.y = 5000 * Math.sin(theta * Math.PI / 180);
				this.positionTabs(theta);
			}
			
			private function positionTabs(camTheta:Number):void {
				var locks:Array = this.getTabLocks();
				var offset:Number = _camTarget - camTheta;
				
				tab1.y = locks[0] - (offset * 30)
				tab1.alpha = 1 - Math.abs(offset) / 7;
				
				tab2.y = locks[1] - (offset * 30)
				tab2.alpha = 1 - Math.abs(offset) / 7;
			}
			
			private function setTabStrings():void {
				if(_camState == "event") {
					tab1.visible = false;
					tab2.visible = true;
					tab2.text = "北京艺术家";					
				} else if(_camState == "creator") {
					tab1.visible = true;
					tab2.visible = true;
					tab1.text = "活动";
					tab2.text = "地图";
				} else {
					tab1.visible = true;
					tab2.visible = false;
					tab1.text = "北京艺术家";
				}
			}
			
			private function onTabClick(event:MouseEvent):void {
				var target:Tab = event.target as Tab;
				
				if(_camState == "event") {
					if(target == tab2) {
						_camTarget = 0;
						_camState = "creator";
						_creatorUI.state = "creators"
						this.addEventListener(Event.ENTER_FRAME, onResetCamera);
						this.setTabStrings();
						cleanInterface();
					}
				} else if(_camState == "creator") {
					if(target == tab1) {
						_camTarget = 14;
						_camState = "event";
						_eventUI.state = "floors"
						this.addEventListener(Event.ENTER_FRAME, onResetCamera);
						this.setTabStrings();
						cleanInterface();
					} else if(target == tab2) {
						_camTarget = -14;
						_camState = "map";
						_mapUI.state = "overview";
						this.addEventListener(Event.ENTER_FRAME, onResetCamera);
						this.setTabStrings();
						cleanInterface();
					}
				} else {
					if(target == tab1) {
						_camTarget = 0;
						_camState = "creator";
						_creatorUI.state = "creators"
						this.addEventListener(Event.ENTER_FRAME, onResetCamera);
						this.setTabStrings();
						cleanInterface();
					}
				}
			}
			
			private function getTabLocks():Array {
				
				return [60, this.frontUI.height - 110]
				
			}
			
			// ____________________________________________ Demos
		]]>
	</mx:Script>
	
	<mx:Canvas id="clickMatte" backgroundColor="0xffffff" width="100%" height="100%" />
	<mx:UIComponent id="backUI" width="100%" height="100%" opaqueBackground="0xffffff"/>
	<mx:Canvas id="frontUI" width="100%" height="100%" horizontalScrollPolicy="off" verticalScrollPolicy="off" />
	
	<mx:HBox id="barTop" height="60" width="100%" x="0" y="0" backgroundColor="0x000000" paddingTop="12" paddingLeft="20">
		<mx:Label id="currentTime" styleName="headerText" />
	</mx:HBox>	
	<mx:Canvas id="barBottom" height="60" width="100%" bottom="0" x="0" backgroundColor="0x000000"
		 horizontalScrollPolicy="off" verticalScrollPolicy="off">
		<mx:Image source="{creatorLogo}" top="12" left="20"/>
		<mx:Image source="{viceLogo}" top="15" right="20"/>
	</mx:Canvas>
	
	<mx:Button id="fullScreenButton" label="Toggle your screen" right="10" bottom = "10" click="this.toggleFullscreen()"/>
	
	<mx:TitleWindow horizontalCenter="0" verticalCenter="0" title="Pick your server poison" id="initWindow">
		<mx:Button label="Run Screen from Localhost" click="init()"/>
		<mx:Button label="Run Screen from Production Server" click="init(false)" />
	</mx:TitleWindow>
	
	
	
</mx:WindowedApplication>
